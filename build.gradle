plugins {
    id 'java-library'
    id 'groovy'
    id 'checkstyle'
    id 'maven-publish'
    
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'org.unbroken-dome.test-sets' version '2.2.1'
}

repositories {
    mavenLocal()
    jcenter()
}

sourceCompatibility = 1.8

group = 'com.finplant.mt_remote'
version = '1.0.0'

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    implementation 'io.projectreactor:reactor-core:3.3.2.RELEASE'
    implementation 'io.projectreactor:reactor-tools:3.3.2.RELEASE'
    implementation 'org.eclipse.jetty.websocket:websocket-client:9.4.24.v20191120'

    testImplementation 'org.codehaus.groovy:groovy-all:2.5.7'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'io.projectreactor:reactor-test:3.3.0.RELEASE'
    testImplementation 'org.assertj:assertj-core:3.11.1'

    checkstyle('com.github.sevntu-checkstyle:sevntu-checks:1.32.0')
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Java client for MTM connector',
                'Implementation-Version': archiveVersion.getOrNull(),
                'Main-Class': project.group + '.Main'
    }
}

shadowJar {
    archiveBaseName.set('mt-remote')
    archiveClassifier.set(null)
}

checkstyle {
    toolVersion = '8.14'
    configFile = new File("checkstyle.xml")
    maxWarnings = 0
    ignoreFailures = false
}

testSets {
    integrationTest { dirName = 'test-integration' }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/SergeyLukashevich/mt-remote-java")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
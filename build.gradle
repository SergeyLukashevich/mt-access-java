buildscript {
    ext {
        shadowVersion = '5.1.0'
        bintrayVersion = '1.8.5'
        testSetsVersion = '2.2.1'
        lombokVersion = '1.18.10'
        reactorVersion = '3.3.5.RELEASE'
        websocketVersion = '1.4.1'

        codeUrl = 'https://github.com/SergeyLukashevich/mt-remote-java'
    }
}

plugins {
    id 'java-library'
    id 'groovy'
    id 'checkstyle'
    id 'maven-publish'
    
    id 'com.github.johnrengelman.shadow' version "${shadowVersion}"
    id 'org.unbroken-dome.test-sets' version "${testSetsVersion}"
    id 'com.jfrog.bintray' version "${bintrayVersion}"
}

repositories {
    mavenLocal()
    jcenter()
}

sourceCompatibility = 1.8

group = 'lv.sergluka.mt_remote'
version = '1.0.0-SNAPSHOT'

dependencies {
    api "io.projectreactor:reactor-core:${reactorVersion}"
    api "io.projectreactor:reactor-tools:${reactorVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    implementation "org.java-websocket:Java-WebSocket:${websocketVersion}"

    testImplementation 'org.codehaus.groovy:groovy-all:2.5.7'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation "io.projectreactor:reactor-test:${reactorVersion}"
    testImplementation 'org.assertj:assertj-core:3.11.1'

    checkstyle('com.github.sevntu-checkstyle:sevntu-checks:1.32.0')
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Java client for MT Remote',
                   'Implementation-Version': archiveVersion.getOrNull(),
                   'Main-Class': project.group + '.Main'
    }
}

shadowJar {
    archiveBaseName.set('mt-remote')
    archiveClassifier.set(null)
}

checkstyle {
    toolVersion = '8.14'
    configFile = new File("checkstyle.xml")
    maxWarnings = 0
    ignoreFailures = false
}

testSets {
    integrationTest {
        dirName = 'test-integration'
    }
}

integrationTest {
    exclude '**/manual/*Tests.groovy'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name 'MIT License'
            url 'http://www.opensource.org/licenses/mit-license.php'
            distribution "repo"
        }
    }
    developers {
        developer {
            id "sergluka"
            name "Sergey Lukashevich"
            email "sergey.lukashevi4@gmail.com"
        }
    }

    scm {
        url "${codeUrl}"
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->

            groupId = 'lv.sergluka.mt-remote'
            artifactId = 'mt-remote'

            project.shadow.component(publication)
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Reactive Java client for the MT Remote')
                root.appendNode('name', 'MT Remote Java library')
                root.appendNode('url', "${codeUrl}")
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.findProperty("bintray.user")
    key = project.findProperty("bintray.key")

    publications = ['shadow']

    pkg {
        repo = 'mt-remote-java'
        name = 'mt-remote-java'
        version {
            name = project.version
            desc = project.version
            released  = new Date()
        }
    }
}



